#compdef grafana-cli
# zsh completions for grafana-cli
# Copyright Â© 2018 Henrik Lindgren
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, see <http://www.gnu.org/licenses/>.

local state line grafana_command

grafana_command=(
  'plugins:Manage plugins for grafana'
  'admin:Grafana admin commands'
  'help:Shows a list of commands or help for one command'
)


_arguments '--pluginsDir[path to grafana plugin directory]:dir:_files -/'\
  '--repo[url to plugin repository]:url:'\
  '--pluginUrl[Full url to plugin zip file instead of downloading plugin from grafana.com/api \[$GF_PLUGIN_URL\]]:zip file url:'\
  '--insecure[Skip TLS verification (insecure)]'\
  {--debug,-d}'[enable debug logging]'\
  {--help,-h}'[show help]'\
  {--version,-v}'[print version]'\
  '1:grafana command:{_describe "grafana command" grafana_command}'\
  '2:grafana sub command:->sub'\
  '3:grafana sub sub command:->sub_sub'


[[ $state = sub ]] && case $words[2] in
  plugins)
    local cmd_plugins
    cmd_plugins=(
      'install:install <plugin id> <plugin version (optional)>'
      'list-remote:list remote available plugins'
      'list-versions:list-versions <plugin id>'
      'update:update <plugin id>'
      'upgrade:upgrade <plugin id>'
      'update-all:update all installed plugins'
      'upgrade-all:upgrade all installed plugins'
      'ls:list all installed plugins'
      'uninstall:uninstall <plugin id>'
      'remove:uninstall <plugin id>'
    )
    _describe 'plugin command' cmd_plugins
    return 0
  ;;

  admin)
  local reset_admin_password
  reset_admin_password=( 'reset-admin-password:reset password <new password>' )
    _describe 'reset admin password' reset_admin_password
    return 0
  ;;
  h|help)
    _describe 'grafana commands' grafana_command
    return 0
  ;;
  *)
    return 0
  ;;
esac

[[ $state =~ sub_sub ]] && [[ $words[2] =~ plugins ]] && case $words[3] in
  install|list-versions)
    local remoteplugins
    remoteplugins=( ${(qf)"$( _call_program grafana grafana-cli plugins list-remote 2>/dev/null |\
      awk '/^id/ {printf "%s:version %s %s\n",$2,$4,$2 }')"})
    _describe 'remote plugins' remoteplugins
    return 0
  ;;
  update|uninstall|remove|upgrade)
    localplugins=( ${(qf)"$( _call_program grafana grafana-cli plugins ls 2>/dev/null |\
      awk '/@/ {printf "%s:version %s %s\n",$1,$2,$1 }')"})
    _describe 'local plugins' localplugins
    return 0
  ;;
esac
return 0

#  vim: set ts=2 sw=2 tw=0 expandtab foldenable foldmethod=syntax foldclose=all :
